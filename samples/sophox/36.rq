# php sample.php sophox 36

select ?etymology_wikidata ?osm_name (GROUP_CONCAT(?osm;SEPARATOR=",") AS ?osm_ids) ?etymology_name ?occupation_ids ?occupation_names
where {
	?osm osmm:type 'w'.
	?osm osmt:name ?osm_name.
	?osm (osmt:name:etymology:wikidata) ?etymology_wikidata.

	SERVICE wikibase:around {
		?osm osmm:loc ?coordinates.
		
		bd:serviceParam wikibase:center "Point(11.69211 44.36452)"^^geo:wktLiteral.
		bd:serviceParam wikibase:radius "2". # kilometers
		bd:serviceParam wikibase:distance ?distance.
	}

	SERVICE <https://query.wikidata.org/sparql> {
        select ?etymology_wikidata
            (SAMPLE(?etymology_name) AS ?etymology_name)
            (GROUP_CONCAT(?occupation;SEPARATOR=",") AS ?occupation_ids)
            (GROUP_CONCAT(?occupation_name;SEPARATOR=", ") AS ?occupation_names)
        where {
            ?etymology_wikidata rdfs:label ?etymology_name.
            FILTER(lang(?etymology_name)="it").
            OPTIONAL {
                ?etymology_wikidata wdt:P106 ?occupation.
                ?occupation rdfs:label ?occupation_name.
                FILTER(lang(?occupation_name)="it").
            }
        }
        group by ?etymology_wikidata
	}
}
group by ?etymology_wikidata ?osm_name ?etymology_name ?occupation_ids ?occupation_names
limit 5
