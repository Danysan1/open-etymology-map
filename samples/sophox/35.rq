# php sample.php sophox 35

select ?name ?etymology_wikidata (GROUP_CONCAT(?osm;SEPARATOR=",") AS ?osm_ids) (GROUP_CONCAT(?occupation;SEPARATOR=",") AS ?occupation_ids) (GROUP_CONCAT(?occupation_name;SEPARATOR=", ") AS ?occupation_names)
where {
	?osm osmm:type 'w'.
	?osm osmt:name ?name.
	?osm (osmt:name:etymology:wikidata) ?etymology_wikidata.

	SERVICE wikibase:around { 
		?osm osmm:loc ?coordinates.
		
		bd:serviceParam wikibase:center "Point(11.69211 44.36452)"^^geo:wktLiteral.
		bd:serviceParam wikibase:radius "2". # kilometers
		bd:serviceParam wikibase:distance ?distance.
	}

	SERVICE <https://query.wikidata.org/sparql> {
		OPTIONAL {
			?etymology_wikidata wdt:P106 ?occupation.
			?occupation rdfs:label ?occupation_name.
			FILTER(lang(?occupation_name)="it")
		}
	}
}
group by ?name ?etymology_wikidata
limit 5
