SELECT ?wikidata
    (SAMPLE(?local_name) AS ?local_name)
    (SAMPLE(?en_name) AS ?en_name)
    (SAMPLE(?all_names) AS ?other_name)
    (COALESCE(SAMPLE(?local_name), SAMPLE(?en_name), SAMPLE(?all_names)) AS ?name)
    (SAMPLE(?description) AS ?description)
    (SAMPLE(?instanceID) AS ?instanceID)
    (SAMPLE(?genderID) AS ?genderID)
    (SAMPLE(?gender_name) AS ?gender)
    (SAMPLE(?wikipedia) AS ?wikipedia)
    (SAMPLE(?commons) AS ?commons)
    (GROUP_CONCAT(DISTINCT ?occupation_name; SEPARATOR=', ') AS ?occupations)
    (GROUP_CONCAT(DISTINCT ?citizenship_name; SEPARATOR=', ') AS ?citizenship)
    (GROUP_CONCAT(DISTINCT ?picture; SEPARATOR='\t') AS ?pictures)
    (GROUP_CONCAT(DISTINCT ?prize_name; SEPARATOR=', ') AS ?prizes)
    (SAMPLE(?event_date) AS ?event_date)
    (SAMPLE(?start_date) AS ?start_date)
    (SAMPLE(?end_date) AS ?end_date)
    (SAMPLE(?birth_date) AS ?birth_date)
    (SAMPLE(?death_date) AS ?death_date)
    (GROUP_CONCAT(DISTINCT ?event_place_name; SEPARATOR=', ') AS ?event_place)
    (SAMPLE(?birth_place_name) AS ?birth_place)
    (SAMPLE(?death_place_name) AS ?death_place)
WHERE {
    VALUES ?wikidata { wd:Q10806 wd:Q166092 wd:Q3840201 wd:Q1067 wd:Q36488 wd:Q1345358 wd:Q1345358 wd:Q185007 wd:Q64707121 wd:Q55433 wd:Q2001 wd:Q6292252 }

    OPTIONAL {
        ?wikidata rdfs:label ?local_name.
        FILTER(lang(?local_name)='it').
    }

    OPTIONAL {
        ?wikidata rdfs:label ?en_name.
        FILTER(lang(?en_name)='en').
    }

    ?wikidata rdfs:label ?all_names.

    OPTIONAL {
        ?wikidata schema:description ?description.
        FILTER(lang(?description)='it').
    }

    OPTIONAL {
        ?wikidata wdt:P106 ?occupation.
        {
            ?wikidata wdt:P21 ?gender.
            FILTER(?gender IN (wd:Q6581072, wd:Q1052281)). # female / transgender female
            #?occupation wdt:P2521 []. # female form of occupation is available
            ?occupation wdt:P2521 ?occupation_name. # female form of occupation label
        } UNION {
            ?wikidata wdt:P21 ?gender.
            FILTER(?gender IN (wd:Q6581072, wd:Q1052281)). # female / transgender female
            MINUS {
                ?occupation wdt:P2521 ?occupation_name.
                FILTER(lang(?occupation_name)='it').
            }. # female form of occupation is NOT available in this language
            ?occupation rdfs:label ?occupation_name. # base occupation label
        } UNION {
            ?wikidata wdt:P21 ?gender.
            FILTER(?gender NOT IN (wd:Q6581072, wd:Q1052281)). # NOT female / transgender female
            #?occupation wdt:P3321 []. # male form of occupation is available
            ?occupation wdt:P3321 ?occupation_name. # male form of occupation label
        } UNION {
            ?wikidata wdt:P21 ?gender.
            FILTER(?gender NOT IN (wd:Q6581072, wd:Q1052281)). # NOT female / transgender female
            MINUS {
                ?occupation wdt:P3321 ?occupation_name.
                FILTER(lang(?occupation_name)='it').
            }. # male form of occupation is NOT available in this language
            ?occupation rdfs:label ?occupation_name. # male form of occupation label
        } UNION {
            ?occupation rdfs:label ?occupation_name. # base occupation label
            MINUS { ?wikidata wdt:P21 []. } . # no gender specified
        }
        FILTER(lang(?occupation_name)='it').
    }

    OPTIONAL {
        {
            ?wikidata wdt:P18 ?picture. # picture
        } UNION {
            ?wikidata wdt:P94 ?picture. # coat of arms image
        }
    }

    OPTIONAL {
        ?wikidata wdt:P31 ?instanceID.
    }

    OPTIONAL {
        ?wikidata wdt:P21 ?genderID.
        ?genderID rdfs:label ?gender_name.
        FILTER(lang(?gender_name)='it').
    }

    OPTIONAL {
        ?wikidata wdt:P585 ?event_date.
    }

    OPTIONAL {
        ?wikidata wdt:P580 ?start_date.
    }

    OPTIONAL {
        ?wikidata wdt:P582 ?end_date.
    }

    OPTIONAL {
        ?wikidata wdt:P569 ?birth_date.
    }

    OPTIONAL {
        ?wikidata wdt:P570 ?death_date.
    }

    OPTIONAL {
        ?wikidata wdt:P276 ?event_place.
        ?event_place rdfs:label ?event_place_name.
        FILTER(lang(?event_place_name)='it').
    }

    OPTIONAL {
        ?wikidata wdt:P19 ?birth_place.
        ?birth_place rdfs:label ?birth_place_name.
        FILTER(lang(?birth_place_name)='it').
    }

    OPTIONAL {
        ?wikidata wdt:P20 ?death_place.
        ?death_place rdfs:label ?death_place_name.
        FILTER(lang(?death_place_name)='it').
    }

    OPTIONAL {
        ?wikipedia schema:about ?wikidata;
            schema:inLanguage ?wikipedia_lang;
            schema:isPartOf [ wikibase:wikiGroup 'wikipedia' ].
        FILTER(?wikipedia_lang = 'it').
    }

    OPTIONAL {
        ?wikidata wdt:P373 ?commons.
    }

    OPTIONAL {
        ?wikidata wdt:P166 ?prize. # awarded prize
        {
            ?prize wdt:P361 ?prize_group.
            FILTER(?prize_group IN (wd:Q7191,wd:Q19020,wd:Q41254)). # Nobel, Academy (Oscar), Grammy
        } UNION {
            ?prize wdt:P31 wd:Q28444913. # Palme d'Or (Cannes)
        } UNION {
            ?prize wdt:P1027 wd:Q49024. # Golden Lion (Venice)
        }
        ?prize rdfs:label ?prize_name.
        FILTER(lang(?prize_name)='it').
    }

    OPTIONAL {
        ?wikidata wdt:P27 ?citizenship.
        ?citizenship rdfs:label ?citizenship_name.
        FILTER(lang(?citizenship_name)='it').
    }
}
GROUP BY ?wikidata
ORDER BY ?wikidata
