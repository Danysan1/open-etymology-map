# php sample.php wikidata 80

SELECT ?wikidata
    (COALESCE(SAMPLE(?name),SAMPLE(?all_names)) AS ?name)
    (SAMPLE(?description) AS ?description)
    (SAMPLE(?gender_name) AS ?gender)
    (SAMPLE(?wikipedia) AS ?wikipedia)
    (GROUP_CONCAT(DISTINCT ?occupation_name;SEPARATOR=', ') AS ?occupations)
    (GROUP_CONCAT(DISTINCT ?demonym_name;SEPARATOR=', ') AS ?citizenships)
    (GROUP_CONCAT(DISTINCT ?picture;SEPARATOR='\t') AS ?pictures)
    (SAMPLE(?birth_date) AS ?birth_date)
    (SAMPLE(?death_date) AS ?death_date)
    (SAMPLE(?birth_place_name) AS ?birth_place)
    (SAMPLE(?death_place_name) AS ?death_place)
    (SAMPLE(?nobel_prize) AS ?nobel_prize)
WHERE {
    VALUES ?wikidata { wd:Q166092 wd:Q3840201 wd:Q1067 wd:Q36488 wd:Q1345358 wd:Q1345358 wd:Q185007 wd:Q64707121 }

    OPTIONAL {
        ?wikidata rdfs:label ?name.
        FILTER(lang(?name)='it').
    }

    ?wikidata rdfs:label ?all_names.

    OPTIONAL {
        ?wikidata schema:description ?description.
        FILTER(lang(?description)='it').
    }

    OPTIONAL {
        ?wikidata wdt:P106 ?occupation.
        {
            ?wikidata wdt:P21 ?gender.
            FILTER(?gender IN (wd:Q6581072, wd:Q1052281)). # female / transgender female
            #?occupation wdt:P2521 []. # female form of occupation is available
            ?occupation wdt:P2521 ?occupation_name. # female form of occupation label
        } UNION {
            ?wikidata wdt:P21 ?gender.
            FILTER(?gender IN (wd:Q6581072, wd:Q1052281)). # female / transgender female
            MINUS {
                ?occupation wdt:P2521 ?occupation_name.
                FILTER(lang(?occupation_name)='it').
            }. # female form of occupation is NOT available in this language
            ?occupation rdfs:label ?occupation_name. # base occupation label
        } UNION {
            ?wikidata wdt:P21 ?gender.
            FILTER(?gender NOT IN (wd:Q6581072, wd:Q1052281)). # NOT female / transgender female
            #?occupation wdt:P3321 []. # male form of occupation is available
            ?occupation wdt:P3321 ?occupation_name. # male form of occupation label
        } UNION {
            ?wikidata wdt:P21 ?gender.
            FILTER(?gender NOT IN (wd:Q6581072, wd:Q1052281)). # NOT female / transgender female
            MINUS {
                ?occupation wdt:P3321 ?occupation_name.
                FILTER(lang(?occupation_name)='it').
            }. # male form of occupation is NOT available in this language
            ?occupation rdfs:label ?occupation_name. # male form of occupation label
        } UNION {
            ?occupation rdfs:label ?occupation_name. # base occupation label
            MINUS { ?wikidata wdt:P21 []. } . # no gender specified
        }
        FILTER(lang(?occupation_name)='it').
    }

    OPTIONAL {
        ?wikidata wdt:P18 ?picture.
    }

    OPTIONAL {
        ?wikidata wdt:P21 ?gender.
        ?gender rdfs:label ?gender_name.
        FILTER(lang(?gender_name)='it').
    }

    OPTIONAL {
        ?wikidata wdt:P569 ?birth_date.
    }

    OPTIONAL {
        ?wikidata wdt:P570 ?death_date.
    }

    OPTIONAL {
        ?wikidata wdt:P19 ?birth_place.
        ?birth_place rdfs:label ?birth_place_name.
        FILTER(lang(?birth_place_name)='it').
    }

    OPTIONAL {
        ?wikidata wdt:P20 ?death_place.
        ?death_place rdfs:label ?death_place_name.
        FILTER(lang(?death_place_name)='it').
    }

    OPTIONAL {
        ?wikipedia schema:about ?wikidata;
            schema:inLanguage ?wikipedia_lang;
            schema:isPartOf [ wikibase:wikiGroup 'wikipedia' ].
        FILTER(?wikipedia_lang = 'it').
    }

    OPTIONAL {
        ?wikidata wdt:P166 ?prize. # awarded prize
        ?prize wdt:P361 wd:Q7191. # nobel prize
        ?prize rdfs:label ?nobel_prize.
        FILTER(lang(?nobel_prize)='it').
    }

    OPTIONAL {
        ?wikidata wdt:P27 ?citizenship.
        ?citizenship p:P1549 ?demonym.
        ?demonym ps:P1549 ?demonym_name.
        #FILTER(lang(?demonym_name)='it').
        ?demonym pq:P518 ?inflection.
        # {
        #     ?wikidata wdt:P21 ?gender.
        #     FILTER(?gender IN (wd:Q6581072,wd:Q1052281)). # female / transgender female
        #     ?demonym wdt:P518 ?inflection.
        #     FILTER(?inflection IN (wd:Q1775415,wd:Q47088293)). # feminine inflection
        # } UNION {
        #     ?wikidata wdt:P21 ?gender.
        #     FILTER(?gender NOT IN (wd:Q6581072,wd:Q1052281)). # NOT female / transgender female
        #     ?demonym wdt:P518 ?inflection.
        #     FILTER(?inflection IN (wd:Q499327,wd:Q47088290)). # masculine inflection
        # } UNION {
        #     MINUS { ?wikidata wdt:P21 []. } . # no gender specified
        #     ?demonym wdt:P518 ?inflection.
        #     FILTER(?inflection IN (wd:Q499327,wd:Q47088290)). # masculine inflection
        # }
    }
}
GROUP BY ?wikidata
ORDER BY ?wikidata
